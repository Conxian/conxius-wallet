apply plugin: 'com.android.application'
// apply plugin: 'kotlin-android'

android {
    namespace "com.conxius.wallet"
    compileSdk 36
    defaultConfig {
        applicationId "com.conxius.wallet"
        minSdk 24
        targetSdk 36
        versionCode 3
        versionName "0.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            // Production Signing Config - Reads from Environment Variables
            // CI/CD pipeline must inject these values.
            storeFile file(System.getenv("KEYSTORE_PATH") ?: "conxius-upload.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: ""
            keyAlias System.getenv("KEY_ALIAS") ?: "conxius-upload"
            keyPassword System.getenv("KEY_PASSWORD") ?: ""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    // kotlinOptions {
    //    jvmTarget = '21'
    // }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packaging {
        resources {
            excludes += ['org/bouncycastle/x509/CertPathReviewerMessages_de.properties']
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.biometric:biometric:1.1.0"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
    implementation('org.bitcoinj:bitcoinj-core:0.16.3') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    implementation "breez_sdk:bindings-android:0.8.0"
    // For RSK / EVM Signing
    implementation('org.web3j:core:4.8.7-android') {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.warn("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
